config {
  type: "view",
  database: constants.PROJECT_ID, 
  schema: "reporting_ecom",
  name: "user_spending",
  /* documentation */
  description: "Create a summary view for recent users including statistics on total spending by product category",
  columns: {
    user_id: "user ID",
    location: "user location",
    category: "product category",
    total_category_spending: "total spending of a product category"
   }
}

select O1.user_id
      , U.location
      , O1.category
      , O1.total_category_spending 
from (select O.user_id
            , P.category
            , coalesce(sum(O.net_sales), 0) as total_category_spending
      from ${ref({name: "orderitems", schema: "staging_ecom"})} as O
       inner join ${ref({name: "products", schema: "staging_ecom"})} as P
        on O.product_id = P.id
      group by O.user_id, P.category) as O1
left join
   (select id as user_id
           , concat(city, ',', country) as location
    from ${ref({name: "users", schema: "staging_ecom"})}
   ) as U
on O1.user_id = U.user_id 
order by O1.user_id ASC, O1.total_category_spending DESC

